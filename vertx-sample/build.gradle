plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '1.2.3'
}

repositories {
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
}

version = '3.4.1'
sourceCompatibility = '1.8'
mainClassName = 'io.vertx.core.Launcher'

dependencies {
    compile "io.vertx:vertx-core:$version"
    compile "io.vertx:vertx-web:$version"
    testCompile "junit:junit:4.11"
    testCompile "io.vertx:vertx-unit:$version"
    testCompile "com.jayway.restassured:rest-assured:2.4.0"
    testCompile "org.assertj:assertj-core:2.0.0"
}

shadowJar {
    classifier = 'fat'
    manifest {
        attributes 'Main-Verticle': 'red.itvirtuoso.vertx.sample.MyFirstVerticle'
    }
    mergeServiceFiles {
        include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.4.1'
}

test.dependsOn.add(shadowJar)
test.doFirst {
    ant.java(jar: "build/libs/${project.name}-${version}-fat.jar",
            fork: true,
            spawn: true)
}
test.doLast {
    ['sh', '-c', "ps -ax | grep ${project.name}-${version}-fat.jar | grep -v grep | awk 'NR==1{print \$1}' | xargs kill -SIGTERM"].execute()
}
